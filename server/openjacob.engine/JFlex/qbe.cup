/*
     This file is part of Open-jACOB
     Copyright (C) 2005-2006 Tarragon GmbH
  
     This program is free software; you can redistribute it and/or modify
     it under the terms of the GNU General Public License as published by
     the Free Software Foundation; version 2 of the License.
  
     This program is distributed in the hope that it will be useful,
     but WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
     GNU General Public License for more details.
  
     You should have received a copy of the GNU General Public License     
     along with this program; if not, write to the Free Software
     Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  
     USA
*/

import org.apache.commons.logging.Log;
import org.apache.commons.logging.LogFactory;

parser code  {: 
  private static Log logger = LogFactory.getLog(QBEParser.class);
  
  static public final transient String RCS_ID = "$Id: qbe.cup,v 1.4 2010/08/06 15:07:00 ibissw Exp $";
  static public final transient String RCS_REV = "$Revision: 1.4 $";
  
  private static String errorToString(String message, Object info)
  {
    StringBuffer m = new StringBuffer("Parser error ");

    if (info instanceof java_cup.runtime.Symbol) 
      m.append("(").append(info.toString()).append(")");

    m.append(" : ").append(message);

    return m.toString();
  }
  
  public void report_error(String message, Object info)
  {
    if (logger.isDebugEnabled())
    {
      logger.debug(errorToString(message, info));
    }
  }
   
  public void report_fatal_error(String message, Object info) throws Exception
  {
    report_error(message, info);
    throw new Exception("Syntax error: " + errorToString(message, info));
  }
:};

terminal TODAY;
terminal NULL;
terminal YTD, QTD, MTD, WTD;
terminal THISY, THISQ, THISM, THISW;
terminal WEEK;
terminal RANGE;
terminal PLUS, MINUS;
terminal LESS_THAN, GREATER_THAN;
terminal LESS_OR_EQUAL_THAN, GREATER_OR_EQUAL_THAN;
terminal NOW;
terminal NOT;
terminal OR, AND;
terminal SINGLEWILDCARD, WILDCARD;
terminal EXACT_MATCH;
terminal LEFT_ANCHOR, RIGHT_ANCHOR;

terminal JANURAY;
terminal FEBRUARY;
terminal MARCH;
terminal APRIL;
terminal MAI;
terminal JUNE;
terminal JULY;
terminal AUGUST;
terminal SEPTEMBER;
terminal OCTOBER;
terminal NOVEMBER;
terminal DECEMBER;

terminal AM, PM;

terminal DOT;
terminal DASH;
terminal COLON;
terminal SLASH;

terminal java.lang.Long        BIGINT_LITERAL;
terminal java.lang.Boolean     BOOLEAN_LITERAL;
terminal java.lang.Integer     INTERVAL_LITERAL;
terminal java.math.BigDecimal  DECIMAL_LITERAL;
terminal java.lang.String      TEXT_LITERAL;
terminal java.lang.String      ENUM_LITERAL;
terminal java.lang.Integer     TWO_DIGIT_LITERAL;
terminal java.lang.Integer     FOUR_DIGIT_LITERAL;
terminal java.lang.String      DATE_LITERAL;

terminal java.lang.Integer     YEARS;
terminal java.lang.Integer     MONTHS;
terminal java.lang.Integer     DAYS;
terminal java.lang.Integer     HOURS;
terminal java.lang.Integer     MINS;
terminal java.lang.Integer     SECS;

non terminal QBEExpression		qbe_start, qbe_expression;
non terminal QBEDualOperator		qbe_logical_operator;
non terminal QBEExpression		qbe_notable_expression;
non terminal QBELiteral			qbe_literal, qbe_numeric_literal, qbe_boolean_literal, qbe_datetime_literal;
non terminal QBEWildcardExpression	wildcard_text_seq;
non terminal QBEWildcard			wildcard_seq;
non terminal QBEWildcard			singlewildcard_seq;

non terminal QBELiteral			timestamp_date_literal;
non terminal QBETimestampLiteral	timestamp_literal;
non terminal QBEDateLiteral 		date_literal;
non terminal dot_opt;
non terminal Integer			month;
non terminal Integer			milliseconds;
non terminal QBETimeLiteral 		time_literal;
non terminal Boolean			is_pm_not_am;

non terminal QBETimeLag			date_time_lag_opt;
non terminal QBETimeLag			timestamp_time_lag_opt;
non terminal QBETimeLag			date_time_lag;
non terminal QBETimeLag			timestamp_time_lag;
non terminal Integer			plus_minus;


/* ensure AND has higher priority than OR */
precedence left OR;
precedence left AND;

start with qbe_start;


/* ------------------- */
/* General declaration */
/* ------------------- */

qbe_start ::= 
{:
	RESULT = null;
:} 
	|	qbe_expression:expr
{:
	RESULT = expr;
:} 
	|	OR qbe_expression:expr
{:
	/* currently we support only one OR group */
	expr.incrementOrGroupNumber();
	RESULT = expr;
:} 
	;
	
qbe_expression ::= 
		qbe_notable_expression:expr
{:
	RESULT = expr;
:} 
	|	NOT qbe_notable_expression:expr
{:
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.NOT, expr);
:} 
	|	qbe_expression:left qbe_logical_operator:op qbe_expression:right
{:
	RESULT = new QBEDualExpression(left, op, right);
:} 
	|	WILDCARD
{:
	/* handle '*' as '!null' */
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.NOT, new QBENullExpression());
:} 
	;
	
qbe_logical_operator ::=
		AND
{:
	RESULT = QBEDualOperator.AND;
:} 
	|	OR
{:
	RESULT = QBEDualOperator.OR;
:} 
	;	
	
qbe_notable_expression ::=
		NULL
{:
	RESULT = new QBENullExpression();
:} 
	|	qbe_numeric_literal:literal
{:
	RESULT = new QBELiteralExpression(literal);
:} 
	|	qbe_boolean_literal:literal
{:
	RESULT = new QBELiteralExpression(literal);
:} 
	|	qbe_datetime_literal:literal
{:
	RESULT = new QBELiteralExpression(literal);
:} 
	|	TEXT_LITERAL:text
{:
	RESULT = new QBETextExpression(text);
:} 
	|	ENUM_LITERAL:text
{:
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.EQUALS, new QBETextLiteral(text));
:} 
	|	EXACT_MATCH TEXT_LITERAL:text
{:
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.EQUALS, new QBETextLiteral(text));
:} 
	|	GREATER_THAN qbe_literal:literal
{:
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.GREATER_THAN, literal);
:} 
	|	GREATER_OR_EQUAL_THAN qbe_literal:literal
{:
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.GREATER_OR_EQUAL_THAN, literal);
:} 
	|	LESS_THAN qbe_literal:literal
{:
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.LESS_THAN, literal);
:} 
	|	LESS_OR_EQUAL_THAN qbe_literal:literal
{:
	RESULT = new QBEUnaryExpression(QBEUnaryOperator.LESS_OR_EQUAL_THAN, literal);
:} 
	|	qbe_numeric_literal:left RANGE qbe_numeric_literal:right
{:
	RESULT = new QBERangeExpression(left, right, true);
:} 
	|	time_literal:left RANGE time_literal:right
{:
	RESULT = new QBERangeExpression(left, right, false);
:} 
	|	timestamp_date_literal:left RANGE timestamp_literal:right
{:
	RESULT = new QBERangeExpression(left, right, false);
:} 
	|	timestamp_date_literal:left RANGE date_literal:right
{:
	// add one day to right operand
	right.increment();
	
	RESULT = new QBERangeExpression(left, right, false);
:} 


/* different date ranges */
	|	YTD date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.YTD);
:} 
	|	QTD date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.QTD);
:} 
	|	MTD date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.MTD);
:} 
	|	WTD date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.WTD);
:} 
	|	THISY date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.THISY);
:} 
	|	THISQ date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.THISQ);
:} 
	|	THISM date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.THISM);
:} 
	|	THISW date_time_lag_opt:lag
{:
	RESULT = new QBEDateRangeExpression(lag, QBEDateRangeExpression.THISW);
:} 

/* different week ranges */
	|	WEEK BIGINT_LITERAL:num date_time_lag_opt:lag
{:
	RESULT = new QBEWeekRangeExpression(num.intValue(), lag);
:} 
	|	WEEK BIGINT_LITERAL:start RANGE WEEK BIGINT_LITERAL:end
{:
	RESULT = new QBEWeekRangeExpression(start.intValue(), end.intValue(), 0);
:} 



	|	LEFT_ANCHOR TEXT_LITERAL:text
{:
	RESULT = new QBEWildcardExpression(text, QBEWildcard.MULTIPLE);
:} 
	|	LEFT_ANCHOR TEXT_LITERAL:text wildcard_text_seq:expr
{:
	expr.addFirst(text); expr.addLast(QBEWildcard.MULTIPLE); RESULT = expr;
:} 
	|	LEFT_ANCHOR singlewildcard_seq:swild TEXT_LITERAL:text
{:
	RESULT = new QBEWildcardExpression(swild, text, QBEWildcard.MULTIPLE);
:} 
	|	LEFT_ANCHOR singlewildcard_seq:swild TEXT_LITERAL:text wildcard_text_seq:expr
{:
	expr.addFirst(text); expr.addFirst(swild); expr.addLast(QBEWildcard.MULTIPLE); RESULT = expr;
:} 

	|	TEXT_LITERAL:text RIGHT_ANCHOR
{:
	RESULT = new QBEWildcardExpression(QBEWildcard.MULTIPLE, text);
:} 
	|	TEXT_LITERAL:text wildcard_text_seq:expr RIGHT_ANCHOR
{:
	expr.addFirst(text); expr.addFirst(QBEWildcard.MULTIPLE); RESULT = expr;
:} 
	|	TEXT_LITERAL:text singlewildcard_seq:swild RIGHT_ANCHOR
{:
	RESULT = new QBEWildcardExpression(QBEWildcard.MULTIPLE, text, swild);
:} 
	|	TEXT_LITERAL:text wildcard_text_seq:expr singlewildcard_seq:swild RIGHT_ANCHOR
{:
	expr.addFirst(text); expr.addFirst(QBEWildcard.MULTIPLE); expr.addLast(swild); RESULT = expr;
:} 

	|	LEFT_ANCHOR TEXT_LITERAL:text RIGHT_ANCHOR
{:
	QBEWildcardExpression expr = new QBEWildcardExpression(text); expr.setAnchored(); RESULT = expr;
:} 
	|	LEFT_ANCHOR TEXT_LITERAL:text wildcard_text_seq:expr RIGHT_ANCHOR
{:
	expr.addFirst(text); expr.setAnchored(); RESULT = expr;
:} 
	|	LEFT_ANCHOR singlewildcard_seq:swild1 TEXT_LITERAL:text RIGHT_ANCHOR
{:
	QBEWildcardExpression expr = new QBEWildcardExpression(swild1, text); expr.setAnchored(); RESULT = expr;
:} 
	|	LEFT_ANCHOR singlewildcard_seq:swild1 TEXT_LITERAL:text wildcard_text_seq:expr RIGHT_ANCHOR
{:
	expr.addFirst(text); expr.addFirst(swild1); RESULT = expr;
:} 
	|	LEFT_ANCHOR TEXT_LITERAL:text singlewildcard_seq:swild2 RIGHT_ANCHOR
{:
	QBEWildcardExpression expr = new QBEWildcardExpression(text, swild2); expr.setAnchored(); RESULT = expr;
:} 
	|	LEFT_ANCHOR TEXT_LITERAL:text wildcard_text_seq:expr singlewildcard_seq:swild2 RIGHT_ANCHOR
{:
	expr.addFirst(text); expr.addLast(swild2); RESULT = expr;
:} 
	|	LEFT_ANCHOR singlewildcard_seq:swild1 TEXT_LITERAL:text singlewildcard_seq:swild2 RIGHT_ANCHOR
{:
	QBEWildcardExpression expr = new QBEWildcardExpression(swild1, text, swild2); expr.setAnchored(); RESULT = expr;
:} 
	|	LEFT_ANCHOR singlewildcard_seq:swild1 TEXT_LITERAL:text wildcard_text_seq:expr singlewildcard_seq:swild2 RIGHT_ANCHOR
{:
	expr.addFirst(text); expr.addFirst(swild1); expr.addLast(swild2); RESULT = expr;
:} 



	|	TEXT_LITERAL:text wildcard_seq:wildcard
{:
	RESULT = new QBEWildcardExpression(text, wildcard);
:} 
	|	TEXT_LITERAL:text wildcard_text_seq:expr
{:
	expr.addFirst(text); RESULT = expr;
:} 
	|	TEXT_LITERAL:text wildcard_text_seq:expr wildcard_seq:wildcard
{:
	expr.addFirst(text); expr.addLast(wildcard); RESULT = expr;
:} 
	|	wildcard_text_seq:expr
{:
	RESULT = expr;
:} 
	|	wildcard_text_seq:expr wildcard_seq:wildcard
{:
	expr.addLast(wildcard); RESULT = expr;
:} 
	;
	
	
qbe_literal ::=
		TEXT_LITERAL:text
{:
	RESULT = new QBETextLiteral(text);
:} 
	|	qbe_numeric_literal:literal
{:
	RESULT = literal;
:} 
	|	qbe_boolean_literal:literal
{:
	RESULT = literal;
:} 
	|	qbe_datetime_literal:literal
{:
	RESULT = literal;
:} 
	;
	
qbe_boolean_literal ::=
		BOOLEAN_LITERAL:bool
{:
	RESULT = new QBEBooleanLiteral(bool);
:} 
	;
	
qbe_numeric_literal ::=
		BIGINT_LITERAL:number
{:
	RESULT = new QBENumericLiteral(number);
:} 
	|	DECIMAL_LITERAL:number
{:
	RESULT = new QBENumericLiteral(number);
:} 
	|	INTERVAL_LITERAL:hours
{:
	RESULT = new QBENumericLiteral(new Long(3600*hours.intValue()));
:} 
	|	INTERVAL_LITERAL:hours COLON INTERVAL_LITERAL:mins
{:
	RESULT = new QBENumericLiteral(new Long(3600*hours.intValue()+60*mins.intValue()));
:} 
	|	INTERVAL_LITERAL:hours COLON INTERVAL_LITERAL:mins COLON INTERVAL_LITERAL:secs
{:
	RESULT = new QBENumericLiteral(new Long(3600*hours.intValue()+60*mins.intValue()+secs.intValue()));
:} 
	;
	
qbe_datetime_literal ::=
		date_literal:literal
{:
	RESULT = literal;
:} 
	|	time_literal:literal
{:
	RESULT = literal;
:} 
	|	timestamp_literal:literal
{:
	RESULT = literal;
:} 
	;
	
wildcard_text_seq ::=
		wildcard_seq:wildcard TEXT_LITERAL:text
{:
	RESULT = new QBEWildcardExpression(wildcard, text);
:} 
	|	wildcard_text_seq:expr wildcard_seq:wildcard TEXT_LITERAL:text
{:
	expr.addLast(wildcard); expr.addLast(text); RESULT = expr;
:} 
	;
	
wildcard_seq ::=
		WILDCARD
{:
	RESULT = QBEWildcard.MULTIPLE;
:} 
	|	singlewildcard_seq:wildcard
{:
	RESULT = wildcard;
:} 
	|	singlewildcard_seq:wildcard WILDCARD
{:
	wildcard.setMultipleTimes(); RESULT = wildcard;
:} 
	|	WILDCARD singlewildcard_seq:wildcard
{:
	wildcard.setMultipleTimes(); RESULT = wildcard;
:} 
	;
	
singlewildcard_seq ::=
		SINGLEWILDCARD
{:
	RESULT = new QBEWildcard();
:} 
	|	singlewildcard_seq:wildcard SINGLEWILDCARD
{:
	wildcard.inc(); RESULT = wildcard;
:} 
	;
	
/* -------------------- */
/* Datetime declaration */
/* -------------------- */

timestamp_date_literal ::=
		timestamp_literal:literal
{:
	RESULT = literal;
:} 
	|	date_literal:literal
{:
	RESULT = literal;
:} 
	;
	
timestamp_literal ::=
		NOW timestamp_time_lag_opt:lag
{:
	RESULT = new QBENowLiteral(lag);
:} 
	|	date_literal:date time_literal:time
{:
	RESULT = new QBETimestampLiteral(date, time);
:} 
	;
	
date_literal ::=
		TODAY date_time_lag_opt:lag
{:
	RESULT = new QBETodayLiteral(lag);
:} 
	|	DATE_LITERAL:str											/* 120205, 12022005 */
{:
	RESULT = new QBEDateLiteral(str);
:} 

	|	TWO_DIGIT_LITERAL:d DOT TWO_DIGIT_LITERAL:m DOT TWO_DIGIT_LITERAL:y			/* 12.02.05 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), true);
:} 
	|	TWO_DIGIT_LITERAL:d DOT TWO_DIGIT_LITERAL:m DOT FOUR_DIGIT_LITERAL:y		/* 12.02.2005, 12.02.0005 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), false);
:} 

	|	TWO_DIGIT_LITERAL:y DASH TWO_DIGIT_LITERAL:m DASH TWO_DIGIT_LITERAL:d 		/* 05-02-12 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), true);
:} 
	|	FOUR_DIGIT_LITERAL:y DASH TWO_DIGIT_LITERAL:m DASH TWO_DIGIT_LITERAL:d 		/* 2005-02-12 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), false);
:} 

	|	TWO_DIGIT_LITERAL:d dot_opt month:m TWO_DIGIT_LITERAL:y						/* 11 Mrz 04, 11. März 04, 04Jan96 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), true);
:} 
	|	TWO_DIGIT_LITERAL:d dot_opt month:m FOUR_DIGIT_LITERAL:y					/* 11 Mrz 2004, 11. März 2004, 04Jan1996 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), false);
:} 

	|	TWO_DIGIT_LITERAL:m SLASH TWO_DIGIT_LITERAL:d SLASH TWO_DIGIT_LITERAL:y		/* 1/4/96 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), true);
:} 
	|	TWO_DIGIT_LITERAL:m SLASH TWO_DIGIT_LITERAL:d SLASH FOUR_DIGIT_LITERAL:y	/* 1/4/1996 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), false);
:} 

	|	month:m TWO_DIGIT_LITERAL:d TWO_DIGIT_LITERAL:y								/* January 4 96, Jan 4 96 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), true);
:} 
	|	month:m TWO_DIGIT_LITERAL:d FOUR_DIGIT_LITERAL:y							/* January 4 1996, Jan 4 1996 */
{:
	RESULT = new QBEDateLiteral(d.intValue(), m.intValue(), y.intValue(), false);
:} 
	;

date_time_lag_opt ::=
	|	date_time_lag:lag
{:
	RESULT = lag;
:} 
	;

timestamp_time_lag_opt ::=
	|	timestamp_time_lag:lag
{:
	RESULT = lag;
:} 
	;
	
date_time_lag ::=
		plus_minus:pm BIGINT_LITERAL:y YEARS
{:
	RESULT = new QBETimeLag(pm.intValue()*y.intValue(), 0, 0, 0, 0, 0);
:} 
	|	plus_minus:pm BIGINT_LITERAL:m MONTHS
{:
	RESULT = new QBETimeLag(0, pm.intValue()*m.intValue(), 0, 0, 0, 0);
:} 
	|	plus_minus:pm BIGINT_LITERAL:d DAYS
{:
	RESULT = new QBETimeLag(0, 0, pm.intValue()*d.intValue(), 0, 0, 0);
:} 
	;
	
timestamp_time_lag ::=
		date_time_lag:lag
{:
	RESULT = lag;
:} 
	|	plus_minus:pm BIGINT_LITERAL:h HOURS
{:
	RESULT = new QBETimeLag(0, 0, 0, pm.intValue()*h.intValue(), 0, 0);
:} 
	|	plus_minus:pm BIGINT_LITERAL:m MINS
{:
	RESULT = new QBETimeLag(0, 0, 0, 0, pm.intValue()*m.intValue(), 0);
:} 
	|	plus_minus:pm BIGINT_LITERAL:s SECS
{:
	RESULT = new QBETimeLag(0, 0, 0, 0, 0, pm.intValue()*s.intValue());
:} 
	;
	
plus_minus ::=
		PLUS
{:
	RESULT = new Integer(1);
:} 
	|	MINUS
{:
	RESULT = new Integer(-1);
:} 
	;	
	
dot_opt ::=
	|	DOT
	;
	
month ::=
		JANURAY
{:
	RESULT = new Integer(1);
:} 
	|	FEBRUARY
{:
	RESULT = new Integer(2);
:} 
	|	MARCH
{:
	RESULT = new Integer(3);
:} 
	|	APRIL
{:
	RESULT = new Integer(4);
:} 
	|	MAI
{:
	RESULT = new Integer(5);
:} 
	|	JUNE
{:
	RESULT = new Integer(6);
:} 
	|	JULY
{:
	RESULT = new Integer(7);
:} 
	|	AUGUST
{:
	RESULT = new Integer(8);
:} 
	|	SEPTEMBER
{:
	RESULT = new Integer(9);
:} 
	|	OCTOBER
{:
	RESULT = new Integer(10);
:} 
	|	NOVEMBER
{:
	RESULT = new Integer(11);
:} 
	|	DECEMBER
{:
	RESULT = new Integer(12);
:} 
	;
	
time_literal ::=
		TWO_DIGIT_LITERAL:h COLON TWO_DIGIT_LITERAL:m							/* 13:00 */	
{:
	RESULT = new QBETimeLiteral(h.intValue(), m.intValue());
:} 
	|	TWO_DIGIT_LITERAL:h COLON TWO_DIGIT_LITERAL:m COLON TWO_DIGIT_LITERAL:s				/* 10:00:00 */	
{:
	RESULT = new QBETimeLiteral(h.intValue(), m.intValue(), s.intValue());
:} 
	|	TWO_DIGIT_LITERAL:h COLON TWO_DIGIT_LITERAL:m COLON TWO_DIGIT_LITERAL:s DOT milliseconds:ms	/* 10:00:00.15 */	
{:
	RESULT = new QBETimeLiteral(h.intValue(), m.intValue(), s.intValue(), ms.intValue());
:} 
	|	TWO_DIGIT_LITERAL:h COLON TWO_DIGIT_LITERAL:m is_pm_not_am:pm					/* 9:40 PM */
{:
	RESULT = new QBETimeLiteral(h.intValue(), m.intValue(), pm.booleanValue());
:} 
	|	TWO_DIGIT_LITERAL:h COLON TWO_DIGIT_LITERAL:m COLON TWO_DIGIT_LITERAL:s is_pm_not_am:pm		/* 9:40:58 AM */
{:
	RESULT = new QBETimeLiteral(h.intValue(), m.intValue(), s.intValue(), pm.booleanValue());
:} 
	|	TWO_DIGIT_LITERAL:h COLON TWO_DIGIT_LITERAL:m COLON TWO_DIGIT_LITERAL:s DOT milliseconds:ms is_pm_not_am:pm	/* 9:40:58.230 AM */
{:
	RESULT = new QBETimeLiteral(h.intValue(), m.intValue(), s.intValue(), ms.intValue(), pm.booleanValue());
:} 
	;
	
milliseconds ::=
		TWO_DIGIT_LITERAL:ms
{:
	RESULT = ms;
:} 
	|	FOUR_DIGIT_LITERAL:ms
{:
	RESULT = ms;
:} 
	;
	
is_pm_not_am ::=
		AM
{:
	RESULT = Boolean.FALSE;
:} 
		
	|	PM
{:
	RESULT = Boolean.TRUE;
:} 
	;	
		
	
	